#!/usr/bin/bpftrace --unsafe

#ifndef BPFTRACE_HAVE_BTF
#include <linux/sched.h>
#endif

/*
 * プロセスの起動と終了を監視するbpftraceスクリプト
 * 起動時にはコマンドライン全体を取得して表示
 */

BEGIN
{
    printf("プロセス監視を開始します...\n");
    printf("%-8s %-20s %-8s %-8s %-8s %s\n", "TIME", "EVENT", "PID", "ELAPSED_SEC", "COMMAND", "CODE");
}

tracepoint:sched:sched_process_exec
/comm == "php"/
{
    $pid = pid;

    // not availabe with lernel 5.15.0
    //printf("cmdpath: %s", args->filename);


    // プロセスIDをキー、現在時刻を値にしてmapを更新
    @start_time[$pid] = nsecs;

    time("%Y-%m-%d %H:%M:%S  ");
    // コマンドラインを取得 (/proc/<pid>/cmdline から読み取り)
    printf("%-20s %-8d %-8s %s / ",
           "EXEC",
           $pid,
           "",
           comm);

    // コマンドライン引数を取得
    system("tr '\\0' ' ' </proc/%d/cmdline", $pid);
    printf("\n");

}

// プロセス終了を監視
tracepoint:sched:sched_process_exit
/comm == "php"/
{
    $task = (struct task_struct *)curtask;
    if ($task->pid == $task->tgid) {
        // プロセスIDをキーにmapが存在した場合に時刻を取り出す
        if (@start_time[pid]) {
            $duration_ns = nsecs - @start_time[pid];
            $duration_sec = $duration_ns / 1000000000;
            time("%Y-%m-%d %H:%M:%S  ");
            printf("%-20s %-8d %-8d %s %d",
            //printf("%d %-7d exit(%d) %s (実行時間: %d秒)\n",
                   "EXIT",
                   pid,
                   $duration_sec,
                   comm,
                   $task->exit_code >> 8
            );
            printf("\n");
            // 終了処理でmapを削除
            delete(@start_time[pid]);
        }
    }
}

END
{
    printf("\n監視を終了します\n");
    // 残っているmapエントリをクリア
    clear(@start_time);
}
